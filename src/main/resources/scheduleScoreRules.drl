package kz.bee.drools.planner.schedule.solver;
    dialect "java"

import org.drools.planner.core.score.buildin.simple.SimpleScoreHolder;
import org.drools.planner.core.score.constraint.UnweightedConstraintOccurrence;

import kz.bee.drools.planner.schedule.domain.Lesson;
import kz.bee.drools.planner.schedule.domain.UnavailablePeriodConstraint;

global SimpleScoreHolder scoreHolder;

//#
// Two lessons in the same room at the same period.
//#
rule "twoLessonSameTimeSameRoom"
	when
		$lesson1 : Lesson($id : id, $period : period, $room : room)
		$lesson2 : Lesson(id != $id, period == $period, room == $room)
		//$lesson1 : Lesson($id : id)
		//$lesson2 : Lesson(id != $id)
	then
		insertLogical(new UnweightedConstraintOccurrence("twoLessonSameTimeSameRoom", $lesson1, $lesson2));
end

//#
// Two classes in the same room at the same period.
//#
rule "twoClassSameTimeSameRoom"
	when
		$lesson1 : Lesson($id : id, $period : period, $room : room, $clazz : course.clazz)
		$lesson2 : Lesson(id != $id, period == $period, room == $room, course.clazz != $clazz)
	then
		insertLogical(new UnweightedConstraintOccurrence("twoClassSameTimeSameRoom", $lesson1, $lesson2));
end

//#
// Two lessons at the same time with a same class.
//#
rule "twoLessonSameTimeSameClass"
	when
		$lesson1 : Lesson($id : id, $period : period, $clazz : course.clazz)
		$lesson2 : Lesson(id != $id, period == $period, course.clazz == $clazz)
	then
		insertLogical(new UnweightedConstraintOccurrence("twoLessonSameTimeSameClass", $lesson1, $lesson2));
end

//#
// Two lessons at the same time or same room with a same teacher.
//#
rule "twoLessonSameTimeSameTeacher"
	when
		$lesson1 : Lesson($id : id, $period : period, $teacher : course.teacher)
		$lesson2 : Lesson(id != $id, period == $period, course.teacher == $teacher)
	then
		insertLogical(new UnweightedConstraintOccurrence("twoLessonSameTimeSameTeacher", $lesson1, $lesson2));
end

// Availabilities: Each lesson in a period unavailable for that course.
rule "unavailablePeriodConstraint"
    when
        $unavailablePeriodConstraint : UnavailablePeriodConstraint($course : course, $period : period)
        $lesson : Lesson(course == $course, period == $period)
    then
    	insertLogical(new UnweightedConstraintOccurrence("twoLessonSameTimeSameTeacher", $unavailablePeriodConstraint, $lesson));
end

// ############################################################################
// Calculate score
// ############################################################################

rule "hardConstraintsBroken"
    when
        $occurrenceCount : Number() from accumulate(
            $unweightedConstraintOccurrence : UnweightedConstraintOccurrence(),
            count($unweightedConstraintOccurrence)
        )
    then
        scoreHolder.setScore(- $occurrenceCount.intValue());
end
